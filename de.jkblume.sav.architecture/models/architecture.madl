import "platform:/plugin/org.qualitune.smags.architecture.dsl/resources/StandardTypes.madl";
import "../../SmagRE/models/AdaptationArchitecture.madl";
import "../../SmagRE/models/CommonLib.madl";
import "../../de.jkblume.sav.sensorml/models/sensorml.madl";

namespace "de.jkblume.sav.architecture";

meta architecture SavMetaArchitecture{
	sensor units:

	type libraries:
	port types:
		
	port type IProcess {
		parameter smlConfiguration : AbstractProcess;
		
		Boolean initialize();
		Boolean validateSmlConfiguration();
		Object execute(Object value);
	}
	
	port type IAggregateProcess extends IProcess{
		requires IProcess[0 to *];
	}
	
	port type ISensor extends IProcess{
		parameter lastEvent : Event;
		
		Object retrieveValues();
		
		requires IProcess[0 to 1];
		
		// helper functions
		String getId();
		Integer getSamplingRate();
		IOPropertyList retrieveOutputStructure();
	}
	
	port type ILogicalSensor extends ISensor{
		requires ISensor[0 to *];
	}
	
	port type IPullThread {
		void start();
		void stop();
		Boolean isRunning();
		
		requires ISensor[0 to 1];
	}
	
	port type ILearningReasoner extends IProcess {
		void startGesture(Category category);
		void stopGesture(Category category);
		void teachCurrentState(Category category);
		void buildClassifier();
		
		requires ISensor[1 to 1];
	}
	
	port type IVisualisationStrategy {
		void visualizeEvent(ISensor source, Event event);
		void addSensor(ISensor sensor);
		void removeSensor(ISensor sensor);
	}
		
	component types:
	
	component type PullThread {
		provides IPullThread;
	}
	
	component type SimpleProcess {
		provides IProcess;
	}
	
	component type AggregateProcess {
		provides IAggregateProcess;
	}
	
	component type TechnicalSensor {
		requires IPullThread;
		provides ISensor;
	}
	
	component type LogicalSensor {
		requires IPullThread;
		provides ILogicalSensor;
	}
	
	component type Visualizer {
		requires ISensor[0 to *];
		provides IVisualisationStrategy;
	}
		
	sensor types:

}